(ns rems.administration.create-form
  "Form editor for administrators used for editing and creating new forms.

  NB: Each field has a generated distinct `:field/id` so that we can track moving fields,
  their answers and especially references between fields. The `:field/id` is optional in
  the API, and would be generated by the backend, but we always send the one we have created.
  We also maintain a separate `:field/index` to automatically number fields for the user's
  benefit.

  NB: We need to sometimes track when a field has been rendered anew. We use the
  `data-field-index` property for this. When a field is e.g. moved, it takes a while for
  React to re-render it. So we want to wait until the element is rendered to the new place
  with the new index before we can scroll to the new position."
  (:require [better-cond.core :as b]
            [clojure.string :as str]
            [medley.core :refer [find-first indexed]]
            [re-frame.core :as rf]
            [reagent.core :as r]
            [reagent.ratom :as ra]
            [rems.administration.administration :as administration]
            [rems.administration.components :refer [checkbox localized-text-field organization-field radio-button-group text-field text-field-inline]]
            [rems.administration.items :as items]
            [rems.atoms :as atoms :refer [document-title]]
            [rems.collapsible :as collapsible]
            [rems.common.form :refer [field-visible? generate-field-id validate-form-template] :as common-form]
            [rems.common.util :refer [not-blank parse-int]]
            [rems.dropdown :as dropdown]
            [rems.fetcher :as fetcher]
            [rems.fields :as fields]
            [rems.flash-message :as flash-message]
            [rems.focus :as focus]
            [rems.spinner :as spinner]
            [rems.text :refer [localized text text-format]]
            [rems.util :refer [navigate! put! post! trim-when-string visibility-ratio focus-input-field]]))

(rf/reg-event-fx
 ::enter-page
 (fn [{:keys [db]} [_ form-id edit-form?]]
   {:db (assoc db
               ::form nil
               ::form-errors nil
               ::preview nil
               ::form-id form-id
               ::edit-form? edit-form?)
    :dispatch-n [(when form-id [::form])]}))

(fetcher/reg-fetcher ::form "/api/forms/:id" {:path-params (fn [db] {:id (::form-id db)})})

;;;; form state

(rf/reg-sub ::edit-form? (fn [db _] (::edit-form? db)))
(rf/reg-sub ::form-errors (fn [db _] (::form-errors db)))
(rf/reg-sub ::form-data (fn [db] (get-in db [::form :data])))
(rf/reg-sub ::fields (fn [db] (vec (get-in db [::form :data :form/fields] []))))
(rf/reg-sub ::find-field-by-id
            :<- [::fields]
            (fn [fields [_ id]]
              (->> (indexed fields)
                   (find-first (comp #{id} :field/id second)))))

(defn- field-editor-id [id] (str "field-editor-" id))

(rf/reg-fx
 ::focus-field-editor
 (fn [field-id]
   (let [editor-id (field-editor-id field-id)]
     (focus/on-element-appear (str "#" editor-id)
                              (fn [element]
                                (focus/scroll-to-top element)
                                (collapsible/open-component editor-id)
                                (r/after-render #(.focus (.querySelector element "textarea"))))))))

(rf/reg-event-fx
 ::add-form-field
 (fn [{:keys [db]} [_ & [index]]]
   (let [new-item (-> db
                      (get-in [::form :data :form/fields])
                      generate-field-id
                      (merge {:field/type :text}))]
     {:db (update-in db [::form :data :form/fields] items/add new-item index)
      ::focus-field-editor (:field/id new-item)})))

(rf/reg-event-db
 ::remove-form-field
 (fn [db [_ field-index]]
   (update-in db [::form :data :form/fields] items/remove field-index)))

(rf/reg-fx ::track-moved-field-editor
           (fn [[field-id index button-selector]]
             (r/after-render
              #(when-some [element (js/document.getElementById (field-editor-id field-id))]
                 (let [before (.getBoundingClientRect element)]
                   ;; NB: the element exists already but we wait for it to reappear with the new index
                   (focus/on-element-appear (str "#" (field-editor-id field-id) "[data-field-index='" index "']")
                                            (fn [element]
                                              (let [after (.getBoundingClientRect element)]
                                                (focus/scroll-offset before after)
                                                (focus/focus-without-scroll (.querySelector element button-selector))))))))))

(rf/reg-event-fx
 ::move-form-field-up
 (fn [{:keys [db]} [_ field-index]]
   (let [field-id (get-in db [::form :data :form/fields field-index :field/id])]
     {:db (update-in db
                     [::form :data :form/fields]
                     items/move-up
                     field-index)
      ::track-moved-field-editor [field-id (dec field-index) ".move-up"]})))

(rf/reg-event-fx
 ::move-form-field-down
 (fn [{:keys [db]} [_ field-index]]
   (let [field-id (get-in db [::form :data :form/fields field-index :field/id])]
     {:db (update-in db
                     [::form :data :form/fields]
                     items/move-down
                     field-index)
      ::track-moved-field-editor [field-id (inc field-index) ".move-down"]})))

(defn- add-item [k db [_ field-index]]
  (update-in db [::form :data :form/fields field-index k] items/add {}))

(defn- remove-item [k db [_ field-index item-index]]
  (update-in db [::form :data :form/fields field-index k] items/remove item-index))

(defn- move-item [k direction db [_ field-index item-index]]
  (case direction
    :up (update-in db [::form :data :form/fields field-index k] items/move-up item-index)
    :down (update-in db [::form :data :form/fields field-index k] items/move-down item-index)))

(rf/reg-event-db ::add-option (partial add-item :field/options))
(rf/reg-event-db ::remove-option (partial remove-item :field/options))
(rf/reg-event-db ::move-option-up (partial move-item :field/options :up))
(rf/reg-event-db ::move-option-down (partial move-item :field/options :down))

(rf/reg-event-db ::add-column (partial add-item :field/columns))
(rf/reg-event-db ::remove-column (partial remove-item :field/columns))
(rf/reg-event-db ::move-column-up (partial move-item :field/columns :up))
(rf/reg-event-db ::move-column-down (partial move-item :field/columns :down))

;;;; form submit

(defn build-localized-string [lstr languages]
  (let [v (into {} (for [language languages]
                     [language (trim-when-string (get lstr language ""))]))]
    (when-not (every? str/blank? (vals v))
      v)))

(defn build-request-field [field languages]
  (let [columns? (common-form/supports-columns? field)
        info-text? (common-form/supports-info-text? field)
        max-length? (common-form/supports-max-length? field)
        optional? (common-form/supports-optional? field)
        options? (common-form/supports-options? field)
        placeholder? (common-form/supports-placeholder? field)
        privacy? (common-form/supports-privacy? field)
        visibility? (common-form/supports-visibility? field)]
    (merge {:field/id (:field/id field)
            :field/title (build-localized-string (:field/title field) languages)
            :field/type (:field/type field)
            :field/optional (if optional?
                              (boolean (:field/optional field))
                              false)}
           (when info-text?
             (when-let [v (build-localized-string (:field/info-text field) languages)]
               {:field/info-text v}))
           (when placeholder?
             (when-let [v (build-localized-string (:field/placeholder field) languages)]
               {:field/placeholder v}))
           (when max-length?
             {:field/max-length (parse-int (:field/max-length field))})
           (when options?
             {:field/options (for [{:keys [key label]} (:field/options field)]
                               {:key key
                                :label (build-localized-string label languages)})})
           (when columns?
             {:field/columns (for [{:keys [key label]} (:field/columns field)]
                               {:key key
                                :label (build-localized-string label languages)})})
           (when privacy?
             (when (= :private (get-in field [:field/privacy]))
               {:field/privacy (:field/privacy field)}))
           (when visibility?
             (when (= :only-if (get-in field [:field/visibility :visibility/type]))
               {:field/visibility (select-keys (:field/visibility field)
                                               [:visibility/type :visibility/field :visibility/values])})))))

(defn build-request [form languages]
  {:organization {:organization/id (get-in form [:organization :organization/id])}
   :form/internal-name (trim-when-string (:form/internal-name form))
   :form/external-title (build-localized-string (:form/external-title form) languages)
   :form/fields (mapv #(build-request-field % languages) (:form/fields form))})

;;;; form validation

(rf/reg-event-fx
 ::create-form
 (fn [{:keys [db]} _]
   (let [request (build-request (get-in db [::form :data]) (:languages db))
         form-errors (validate-form-template request (:languages db))]
     (when-not form-errors
       (post! "/api/forms/create"
              {:params request
               :handler (flash-message/default-success-handler :top
                                                               [text :t.administration/create-form]
                                                               #(navigate! (str "/administration/forms/" (:id %))))
               :error-handler (flash-message/default-error-handler :top
                                                                   [text :t.administration/create-form])}))
     {:db (assoc db ::form-errors form-errors)})))

(rf/reg-event-fx
 ::edit-form
 (fn [{:keys [db]} _]
   (let [request (build-request (get-in db [::form :data]) (:languages db))
         form-errors (validate-form-template request (:languages db))]
     (when-not form-errors
       (put! "/api/forms/edit"
             {:params (assoc request :form/id (::form-id db))
              :handler (flash-message/default-success-handler :top
                                                              [text :t.administration/edit-form]
                                                              #(navigate! (str "/administration/forms/" (::form-id db))))
              :error-handler (flash-message/default-error-handler :top
                                                                  [text :t.administration/edit-form])}))
     {:db (assoc db ::form-errors form-errors)})))

;;;; preview auto-scrolling

(defn true-height [element]
  (let [style (-> js/window
                  (.getComputedStyle element))]
    (+ (.-offsetHeight element)
       (parse-int (.-marginTop style))
       (parse-int (.-marginBottom style)))))

(defn set-visibility-ratio [frame element ratio]
  (when (and frame element)
    (let [element-top (- (.-offsetTop element) (.-offsetTop frame))
          element-height (true-height element)
          top-margin (/ (.-offsetHeight frame) 4)
          position (+ element-top
                      element-height
                      (* -1 ratio element-height)
                      (- top-margin))]
      (-> frame
          (.scrollTo 0 position)))))

(defn first-partially-visible-edit-field []
  (let [fields (-> js/document
                   (.querySelectorAll "#create-form .form-field:not(.new-form-field)")
                   array-seq)
        is-visible? (fn [^js elem]
                      (<= 0 (-> elem .getBoundingClientRect .-bottom)))]
    (->> fields
         (find-first is-visible?))))

(defn autoscroll []
  (b/when-some [edit-field (first-partially-visible-edit-field)
                id (last (str/split (.-id edit-field) #"-"))
                preview-frame (.querySelector js/document "#preview-form-contents")
                preview-field (-> js/document
                                  (.getElementById (str "field-preview-" id)))
                ratio (visibility-ratio edit-field)]
    (r/after-render
     #(set-visibility-ratio preview-frame
                            preview-field
                            ratio))))

(def ^:private use-autoscroll
  "Hook-like helper that attaches autoscroll event listener on component mount,
   and removes it on unmount."
  (ra/make-reaction #(.addEventListener js/window "scroll" autoscroll)
                    {:on-dispose #(.removeEventListener js/window "scroll" autoscroll)}))

;;;; UI

(rf/reg-sub ::get-form :<- [::form-data] (fn [form [_ key-path]] (get-in form key-path)))
(rf/reg-sub ::get-form-error :<- [::form-errors] (fn [errors [_ key-path]] (get-in errors key-path)))
(rf/reg-event-db ::update-form (fn [db [_ keys value]] (assoc-in db (concat [::form :data] keys) value)))

(def ^:private context
  {:get-form ::get-form
   :get-form-error ::get-form-error
   :update-form ::update-form})

(defn- form-organization-field []
  [organization-field context {:keys [:organization]}])

(defn- form-internal-name-field []
  [text-field context {:keys [:form/internal-name]
                       :label [text :t.administration/internal-name]}])

(defn- form-external-title-field []
  [localized-text-field context {:keys [:form/external-title]
                                 :label [text :t.administration/external-title]}])

(defn- form-field-id-field [field-index]
  [text-field-inline context {:keys [:form/fields field-index :field/id]
                              :label [text :t.create-form/field-id]}])

(defn- form-field-title-field [field-index]
  [localized-text-field context {:keys [:form/fields field-index :field/title]
                                 :label [text :t.create-form/field-title]}])

(defn- form-field-placeholder-field [field-index]
  [localized-text-field context {:keys [:form/fields field-index :field/placeholder]
                                 :collapse? true
                                 :label [text :t.create-form/placeholder]}])

(defn- form-field-info-text [field-index]
  [localized-text-field context {:keys [:form/fields field-index :field/info-text]
                                 :collapse? true
                                 :label [text :t.create-form/info-text]}])

(defn- form-field-max-length-field [field-index]
  [text-field-inline context {:keys [:form/fields field-index :field/max-length]
                              :label [text :t.create-form/maxlength]}])

(defn- form-field-option-field [field-index option-index]
  [:div.form-field-option

   [:div.form-field-header
    [:h4 (text-format :t.create-form/option-n (inc option-index))]
    [:div.form-field-controls
     [items/move-up-button {:on-click #(rf/dispatch [::move-option-up field-index option-index])
                            :class (str "fields-" field-index "-option-" option-index)}]
     [items/move-down-button {:on-click #(rf/dispatch [::move-option-down field-index option-index])
                              :class (str "fields-" field-index "-option-" option-index)}]
     [items/remove-button #(rf/dispatch [::remove-option field-index option-index])]]]

   [text-field-inline context {:keys [:form/fields field-index :field/options option-index :key]
                               :label [text :t.create-form/option-key]
                               :normalizer common-form/normalize-option-key}]
   [localized-text-field context {:keys [:form/fields field-index :field/options option-index :label]
                                  :label [text :t.create-form/option-label]}]])

(defn- form-field-option-fields [field-index]
  (let [options @(rf/subscribe [::get-form [:form/fields field-index :field/options]])]
    [:div
     (into [:<>] (for [option-index (range (count options))]
                   [form-field-option-field field-index option-index]))
     [:div.form-field-option.new-form-field-option
      [atoms/action-link {:class "add-option"
                          :id (str "fields-" field-index "-add-option")
                          :label [:<>
                                  [atoms/add-symbol] " " [text :t.create-form/add-option]]
                          :on-click #(rf/dispatch [::add-option field-index])}]]]))

;; TODO column code is an exact duplication of option code
;; TODO column code reuses some option styles

(defn- add-form-field-column-button [field-index]
  [:a.add-option {:href "#"
                  :id (str "fields-" field-index "-add-column")
                  :on-click (fn [event]
                              (.preventDefault event)
                              (rf/dispatch [::add-column field-index]))}
   [text :t.create-form/add-column]])

(defn- remove-form-field-column-button [field-index column-index]
  [items/remove-button #(rf/dispatch [::remove-column field-index column-index])])

(defn- move-form-field-column-up-button [field-index column-index]
  [items/move-up-button
   {:on-click #(rf/dispatch [::move-column-up field-index column-index])}])

(defn- move-form-field-column-down-button [field-index column-index]
  [items/move-down-button
   {:on-click #(rf/dispatch [::move-column-down field-index column-index])}])

(defn- form-field-column-field [field-index column-index]
  [:div.form-field-option
   [:div.form-field-header
    [:h4
     [text-format :t.create-form/column-n (inc column-index)]]
    [:div.form-field-controls
     [move-form-field-column-up-button field-index column-index]
     [move-form-field-column-down-button field-index column-index]
     [remove-form-field-column-button field-index column-index]]]
   [text-field-inline context {:keys [:form/fields field-index :field/columns column-index :key]
                               :label [text :t.create-form/column-key]
                               :normalizer common-form/normalize-option-key}]
   [localized-text-field context {:keys [:form/fields field-index :field/columns column-index :label]
                                  :label [text :t.create-form/column-label]}]])

(defn- form-field-column-fields [field-index]
  (let [columns @(rf/subscribe [::get-form [:form/fields field-index :field/columns]])]
    [:div {:id (str "fields-" field-index "-columns")}
     (into [:<>]
           (for [column-index (range (count columns))]
             [form-field-column-field field-index column-index]))
     [:div.form-field-option.new-form-field-option
      [add-form-field-column-button field-index]]]))

(defn- can-select-visibility? [field]
  (contains? #{:option :multiselect} (:field/type field)))

(rf/reg-sub ::visibility-form-fields
            :<- [::fields]
            (fn [form-fields]
              (filterv can-select-visibility? form-fields)))

(rf/reg-event-db
 ::form-field-visibility-type
 (fn [db [_ field-index visibility-type]]
   (assoc-in db [::form :data :form/fields field-index :field/visibility :visibility/type] visibility-type)))

(rf/reg-event-db
 ::form-field-visibility-field
 (fn [db [_ field-index visibility-field]]
   (assoc-in db [::form :data :form/fields field-index :field/visibility :visibility/field] visibility-field)))

(rf/reg-event-db
 ::form-field-visibility-values
 (fn [db [_ field-index visibility-value]]
   (assoc-in db [::form :data :form/fields field-index :field/visibility :visibility/values] visibility-value)))

(defn- form-field-visibility
  "Component for specifying form field visibility rules"
  [field-index]
  (let [error @(rf/subscribe [::get-form-error [:form/fields field-index :field/visibility]])
        error-type (:visibility/type error)
        error-field (:visibility/field error)
        error-value (:visibility/values error)

        id-type (str "fields-" field-index "-visibility-type")
        id-field (str "fields-" field-index "-visibility-field")
        id-value (str "fields-" field-index "-visibility-value")

        label-type (text :t.create-form/type-visibility)
        label-field (text :t.create-form.visibility/field)
        label-value (text :t.create-form.visibility/has-value)

        visibility @(rf/subscribe [::get-form [:form/fields field-index :field/visibility]])
        visibility-type (:visibility/type visibility)
        visibility-field (:visibility/field visibility)
        visibility-value (:visibility/values visibility)]
    [:div {:class (when (= :only-if visibility-type)
                    "form-field-visibility")}
     [:div.form-group.field.row {:id (str "container-field" field-index)}
      [:label.col-sm-3.col-form-label {:for id-type} label-type]
      [:div.col-sm-9
       [:select.form-control
        {:id id-type
         :class (when error-type "is-invalid")
         :on-change #(rf/dispatch [::form-field-visibility-type field-index (keyword (.. % -target -value))])
         :value (or visibility-type "")}
        [:option {:value "always"} (text :t.create-form.visibility/always)]
        [:option {:value "only-if"} (text :t.create-form.visibility/only-if)]]
       [:div.invalid-feedback
        (some-> error-type (text-format label-type))]]]
     (when (= :only-if visibility-type)
       [:<>
        [:div.form-group.field.row
         [:label.col-sm-3.col-form-label {:for id-field} label-field]
         [:div.col-sm-9
          [:select.form-control
           {:id id-field
            :class (when error-field "is-invalid")
            :on-change #(rf/dispatch [::form-field-visibility-field field-index {:field/id (.. % -target -value)}])
            :value (or (:field/id visibility-field) "")}
           (into [:<>
                  [:option ""]]
                 (for [field @(rf/subscribe [::visibility-form-fields])
                       :let [index (first @(rf/subscribe [::find-field-by-id (:field/id field)]))]]
                   [:option {:value (:field/id field)}
                    (text-format :t.create-form/field-n
                                 (inc index)
                                 (localized (:field/title field)))]))]
          [:div.invalid-feedback
           (some-> error-field (text-format label-field))]]]
        (when-let [visibility-field-id (:field/id visibility-field)]
          [:div.form-group.field.row
           [:label.col-sm-3.col-form-label {:for id-value} label-value]
           [:div.col-sm-9
            [dropdown/dropdown
             {:id id-value
              :items (vec (for [option (-> @(rf/subscribe [::find-field-by-id visibility-field-id])
                                           second
                                           :field/options)]
                            {:value (:key option)
                             ::label (localized (:label option))}))
              :item-key #(str field-index "-" %)
              :item-label ::label
              :item-selected? #(contains? (set visibility-value) (:value %))
              :multi? true
              :clearable? true
              :on-change #(rf/dispatch [::form-field-visibility-values field-index (mapv :value %)])}]
            [:div.invalid-feedback
             (some-> error-value (text-format label-value))]]])])]))

(defn- form-field-privacy
  "Component for specifying form field privacy rules.

  Privacy concerns the reviewers as they can see only public fields."
  [field-index]
  (let [privacy @(rf/subscribe [::get-form [:form/fields field-index :field/privacy]])
        error @(rf/subscribe [::get-form-error [:form/fields field-index :field/privacy]])
        id (str "fields-" field-index "-privacy-type")]
    [:div.form-group.field.row {:id (str "container-field" field-index)}
     [:label.col-sm-2.col-form-label {:for id}
      [text :t.create-form/type-privacy]]
     [:div.col-sm-10
      [:select.form-control
       {:id id
        :class (when error "is-invalid")
        :on-change #(let [new-value (keyword (.. % -target -value))]
                      (rf/dispatch [::set-form-field [:form/fields field-index :field/privacy] new-value]))
        :value (or privacy "public")}
       [:option {:value "public"} (text :t.create-form.privacy/public)]
       [:option {:value "private"} (text :t.create-form.privacy/private)]]]]))

(defn- form-field-type-radio-group [field-index]
  [radio-button-group context
   {:id (str "radio-group-" field-index)
    :keys [:form/fields field-index :field/type]
    :label [text :t.create-form/field-type]
    :orientation :horizontal
    :options [{:value :description :label [text :t.create-form/type-description]}
              {:value :text :label [text :t.create-form/type-text]}
              {:value :texta :label [text :t.create-form/type-texta]}
              {:value :option :label [text :t.create-form/type-option]}
              {:value :multiselect :label [text :t.create-form/type-multiselect]}
              {:value :table :label [text :t.create-form/type-table]}
              {:value :date :label [text :t.create-form/type-date]}
              {:value :email :label [text :t.create-form/type-email]}
              {:value :phone-number :label [text :t.create-form/type-phone-number]}
              {:value :ip-address :label [text :t.create-form/type-ip-address]}
              {:value :attachment :label [text :t.create-form/type-attachment]}
              {:value :label :label [text :t.create-form/type-label]}
              {:value :header :label [text :t.create-form/type-header]}]}])

(defn- form-field-optional-checkbox [field-index]
  [checkbox context {:keys [:form/fields field-index :field/optional]
                     :label [text :t.create-form/optional]}])

(defn- form-field-table-optional-checkbox [field-index]
  [checkbox context {:keys [:form/fields field-index :field/optional]
                     :negate? true
                     :label [text :t.create-form/required-table]}])

(defn- add-form-field-button [index]
  [:a.add-form-field {:href "#"
                      :on-click (fn [event]
                                  (.preventDefault event)
                                  (rf/dispatch [::add-form-field index]))}
   [atoms/add-symbol] " " [text :t.create-form/add-form-field]])

(defn- save-form-button [on-click]
  [:button.btn.btn-primary
   {:type :button
    :id :save
    :on-click (fn []
                (rf/dispatch [:rems.spa/user-triggered-navigation]) ;; scroll to top
                (on-click))}
   (text :t.administration/save)])

(defn- cancel-button []
  [atoms/link {:class "btn btn-secondary"}
   "/administration/forms"
   (text :t.administration/cancel)])

(defn- format-validation-link [target content]
  [:li [:a {:href "#" :on-click (focus-input-field target)}
        content]])

(defn- format-error-for-localized-field [error label lang]
  (text-format error (str (text label) " (" (str/upper-case (name lang)) ")")))

(defn- format-field-validation [field field-errors]
  (let [field-index (:field/index field)]
    [:li (text-format :t.create-form/field-n
                      (inc field-index)
                      (localized (:field/title field)))
     (into [:ul]
           (concat
            (for [[lang error] (:field/title field-errors)]
              (format-validation-link (str "fields-" field-index "-title-" (name lang))
                                      (format-error-for-localized-field error :t.create-form/field-title lang)))
            (for [[lang error] (:field/placeholder field-errors)]
              (format-validation-link (str "fields-" field-index "-placeholder-" (name lang))
                                      (format-error-for-localized-field error :t.create-form/placeholder lang)))
            (for [[lang error] (:field/info-text field-errors)]
              (format-validation-link (str "fields-" field-index "-info-text-" (name lang))
                                      (format-error-for-localized-field error :t.create-form/info-text lang)))
            (when (:field/max-length field-errors)
              [(format-validation-link (str "fields-" field-index "-max-length")
                                       (str (text :t.create-form/maxlength) ": " (text (:field/max-length field-errors))))])
            (when (-> field-errors :field/visibility :visibility/type)
              [(format-validation-link (str "fields-" field-index "-visibility-type")
                                       (str (text :t.create-form/type-visibility) ": " (text-format (-> field-errors :field/visibility :visibility/type) (text :t.create-form/type-visibility))))])
            (when (-> field-errors :field/visibility :visibility/field)
              [(format-validation-link (str "fields-" field-index "-visibility-field")
                                       (str (text :t.create-form/type-visibility) ": " (text-format (-> field-errors :field/visibility :visibility/field) (text :t.create-form.visibility/field))))])
            (when (-> field-errors :field/visibility :visibility/values)
              [(format-validation-link (str "fields-" field-index "-visibility-value")
                                       (str (text :t.create-form/type-visibility) ": " (text-format (-> field-errors :field/visibility :visibility/values) (text :t.create-form.visibility/has-value))))])
            (if (= :t.form.validation/options-required (:field/options field-errors))
              [[:li
                [:a {:href "#" :on-click #(focus/focus-selector (str "#fields-" field-index "-add-option"))}
                 (text :t.form.validation/options-required)]]]
              (for [[option-id option-errors] (into (sorted-map) (:field/options field-errors))]
                [:li (text-format :t.create-form/option-n (inc option-id))
                 [:ul
                  (when (:key option-errors)
                    (format-validation-link (str "fields-" field-index "-options-" option-id "-key")
                                            (text-format (:key option-errors) (text :t.create-form/option-key))))
                  (into [:<>]
                        (for [[lang error] (:label option-errors)]
                          (format-validation-link (str "fields-" field-index "-options-" option-id "-label-" (name lang))
                                                  (format-error-for-localized-field error :t.create-form/option-label lang))))]]))
            (if (= :t.form.validation/columns-required (:field/columns field-errors))
              [[:li
                [:a {:href "#" :on-click #(focus/focus-selector (str "#fields-" field-index "-add-column"))}
                 (text :t.form.validation/columns-required)]]]
              (for [[column-id column-errors] (into (sorted-map) (:field/columns field-errors))]
                [:li (text-format :t.create-form/column-n (inc column-id))
                 [:ul
                  (when (:key column-errors)
                    (format-validation-link (str "fields-" field-index "-columns-" column-id "-key")
                                            (text-format (:key column-errors) (text :t.create-form/option-key))))
                  (into [:<>]
                        (for [[lang error] (:label column-errors)]
                          (format-validation-link (str "fields-" field-index "-columns-" column-id "-label-" (name lang))
                                                  (format-error-for-localized-field error :t.create-form/option-label lang))))]]))))]))

;; XXX: shared component with rems.administration.form
(defn format-validation-errors [form-errors form]
  ;; TODO: deduplicate with field definitions
  [:ul
   (when-let [error (:organization form-errors)]
     (format-validation-link "organization"
                             (text-format error (text :t.administration/organization))))
   (when-let [error (:form/internal-name form-errors)]
     (format-validation-link "internal-name"
                             (text-format error (text :t.administration/internal-name))))
   (into [:<>]
         (for [[lang error] (:form/external-title form-errors)]
           (format-validation-link (str "external-title-" (name lang))
                                   (format-error-for-localized-field error :t.administration/external-title lang))))
   (into [:<>]
         (for [[field-index field-errors] (into (sorted-map) (:form/fields form-errors))]
           (let [field (get-in form [:form/fields field-index])]
             [format-field-validation field field-errors])))])

(defn- validation-errors-summary []
  (when-some [errors (not-empty @(rf/subscribe [::form-errors]))]
    [:div.alert.alert-danger (text :t.actions.errors/validation-errors)
     [format-validation-errors errors @(rf/subscribe [::form-data])]]))

(defn- field-controls [idx title]
  [:div.form-field-header.d-flex
   [:h3 (text-format :t.create-form/field-n (inc idx) (localized title))]
   [:div.form-field-controls.text-nowrap.ml-auto
    [items/move-up-button {:on-click #(rf/dispatch [::move-form-field-up idx])}]
    [items/move-down-button {:on-click #(rf/dispatch [::move-form-field-down idx])}]
    [items/remove-button (let [label (text :t.create-form/confirm-remove-field)]
                           #(when (js/confirm label)
                              (rf/dispatch [::remove-form-field idx])))]]])

(defn- field-editor [idx field]
  (let [editor-id (field-editor-id (:field/id field))]
    [:div.form-field {:id editor-id
                      :data-field-index idx}
     [collapsible/minimal
      {:id editor-id ; not the same as element id
       :always [field-controls idx (:field/title field)]
       :collapse (let [columns? (common-form/supports-columns? field)
                       info-text? (common-form/supports-info-text? field)
                       max-length? (common-form/supports-max-length? field)
                       optional? (common-form/supports-optional? field)
                       options? (common-form/supports-options? field)
                       placeholder? (common-form/supports-placeholder? field)
                       privacy? (common-form/supports-privacy? field)
                       visibility? (common-form/supports-visibility? field)]
                   [:div {:id (str editor-id "-contents")
                          :tab-index "-1"}
                    [form-field-title-field idx]
                    [form-field-type-radio-group idx]

                    (when optional?
                      (if (= :table (:field/type field))
                        [form-field-table-optional-checkbox idx]
                        [form-field-optional-checkbox idx]))
                    (when info-text?
                      [form-field-info-text idx])
                    (when placeholder?
                      [form-field-placeholder-field idx])

                    [:div.form-group.field
                     [:label.administration-field-label
                      (text :t.create-form/additional-settings)
                      " "
                      [collapsible/controls (str "fields-" idx "-additional")]]
                     [:div.collapse.solid-group {:id (str "fields-" idx "-additional")}
                      [form-field-id-field idx]
                      (when max-length?
                        [form-field-max-length-field idx])
                      (when privacy?
                        [form-field-privacy idx])
                      (when visibility?
                        [form-field-visibility idx])]]

                    (when options?
                      [form-field-option-fields idx])
                    (when columns?
                      [form-field-column-fields idx])])}]]))

(defn- get-field-key [field idx]
  (or (not-blank (:field/id field))
      (str "new-field-" idx)))

(defn- form-fields []
  (into [:<>
         [:div.new-form-field
          [add-form-field-button 0]]]
        (for [[idx field] (indexed @(rf/subscribe [::fields]))]
          ^{:key (get-field-key field idx)}
          [:<>
           [field-editor idx field]
           [:div.new-form-field
            [add-form-field-button (inc idx)]]])))

(rf/reg-event-db
 ::set-field-value
 (fn [db [_ field-id field-value]]
   (assoc-in db [::preview field-id] field-value)))

;; currently, field can be passed from a) component props, or b) subscription.
;; when a), form fields are not fetchable from db by field id, so must use whole object instead.
(rf/reg-sub ::preview (fn [db [_ field]]
                        (let [preview (::preview db)
                              visible? (field-visible? field (::preview db))]
                          {:value (get preview (:field/id field))
                           ::field-visible? visible?})))

(defn- render-preview-hidden []
  [:div {:style {:position :absolute
                 :top 0
                 :left 0
                 :right 0
                 :bottom 0
                 :z-index 1
                 :display :flex
                 :flex-direction :column
                 :justify-content :center
                 :align-items :flex-end
                 :border-radius "0.4rem"
                 :margin "-0.5rem"
                 :background-color "rgba(230,230,230,0.5)"}}
   [:div.pr-4 (text :t.create-form.visibility/hidden)]])

(defn- preview-field [field]
  (let [field-id (:field/id field)
        preview @(rf/subscribe [::preview field])]
    [:div.field-preview {:id (str "field-preview-" field-id)}
     [fields/field (assoc field
                          :form/id 1 ; dummy value
                          :on-change (r/partial #(rf/dispatch-sync [::set-field-value field-id %]))
                          :field/value (:value preview))]
     (when-not (::field-visible? preview)
       [render-preview-hidden])]))

(rf/reg-event-db ::reset-preview (fn [db] (assoc db ::preview nil)))

(def ^:private use-preview-state
  "Hook-like helper that resets form preview state on component unmount."
  (ra/make-reaction identity
                    {:on-dispose #(rf/dispatch [::reset-preview])}))

;; XXX: shared component with rems.administration.form
(defn form-preview [& [form]]
  @use-preview-state

  (let [form-fields (if form
                      (:form/fields form)
                      @(rf/subscribe [::fields]))]
    [collapsible/component
     {:id "preview-form"
      :title (text :t.administration/preview)
      :always (into [:div#preview-form-contents]
                    (for [[idx field] (indexed form-fields)]
                      ^{:key (get-field-key field idx)}
                      [preview-field field]))}]))

(defn- form-page-wrapper []
  @use-autoscroll

  [:div.row
   [:div.col-lg
    [collapsible/component
     {:id "create-form"
      :class "fields"
      :title (text :t.administration/form)
      :always [:div
               [form-organization-field]
               [form-internal-name-field]
               [form-external-title-field]
               [form-fields]
               [:div.col.commands
                [cancel-button]
                [save-form-button (if @(rf/subscribe [::edit-form?])
                                    #(rf/dispatch [::edit-form])
                                    #(rf/dispatch [::create-form]))]]]}]]
   [:div.col-lg [form-preview]]])

(defn create-form-page []
  (let [editing? @(rf/subscribe [::edit-form?])]
    [:div
     [administration/navigator]
     [document-title (if editing?
                       (text :t.administration/edit-form)
                       (text :t.administration/create-form))]
     [flash-message/component :top]
     (cond
       (and editing? (not @(rf/subscribe [::form :initialized?])))
       [spinner/big]

       (and editing? @(rf/subscribe [::form :fetching?]))
       [spinner/big]

       :else
       [:<>
        [validation-errors-summary]
        [form-page-wrapper]])]))
